/*------------------------------------------------------------------------------
  HEIG-Vd - CoE@SNU              Summer University              July 11-22, 2016

  The Art of Compiler Construction


  suPL scanner definition

*/

%option yylineno

%{
#include "supl.tab.h"       // token definitions and yylval - generated by bison

int yycolumn = 1;

#define YY_USER_ACTION {                            \
  yylloc.first_line = yylloc.last_line = yylineno;  \
  yylloc.first_column = yycolumn;                   \
  yylloc.last_column = yycolumn + yyleng - 1;       \
  yycolumn += yyleng;                               \
}
%}

DIGIT     [0-9]
ALPHA     [A-Za-z_]
STR    	  \"([ -~]|\\n|\\t|\\\"|\\)*\"

%%

int                       { return(INTEGER); }
void                      { return(VOID); }
if 						  { return(IF); }
else 					  { return(ELSE); }
while 					  { return(WHILE); }
return 					  { return(RETURN); }
read 					  { return(READ); }
write 					  { return(WRITE); }
print 					  { return(PRINT); }

{ALPHA}({ALPHA}|{DIGIT})* { yylval.str = strdup(yytext); return(IDENT); }
{DIGIT}+                  { yylval.n = atoi(yytext);     return(NUMBER); }
{STR} 				  	  {
							char buf[2048] = {0,};
							int j = 0;
							for(int i=1; i<strlen(yytext)-1; i++) {
								if(i < strlen(yytext)-2) {
									if(yytext[i] == '\\' && yytext[i+1] == 'n') {
										buf[j++] = '\n';
										i++;
									} else if(yytext[i] == '\\' && yytext[i+1] == 't') {
										buf[j++] = '\t';
										i++;
									} else if(yytext[i] == '\\' && yytext[i+1] == '\"') {
										buf[j++] = '\"';
										i++;
									} else {
										buf[j++] = yytext[i];
									}
								} else {
									buf[j++] = yytext[i];
								}
							}
							yylval.str = strdup(buf);
							//yylval.str = strndup(&yytext[1], strlen(yytext)-2);
						 	return(STRING);
						  }

[ \t]+                    // ignore whitespace
[\n]+                     { yycolumn = 1; }           // reset column on newlines
.                         { return(yytext[0]); }

%%
